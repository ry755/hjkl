/// The entry point of the program. This passes the input/output file and the first 3 command line arguments from the stack (where fox32os puts them) into registers (where the Jackal calling convention expects them). The fifth parameter, the fourth command line argument, remains on the stack. Then the Main function is called, which must not return.
#ASM [
    pop a0 // streamPtr
    pop a1 // arg0
    pop a2 // arg1
    pop a3 // arg2
    // arg3 is now on the top of stack
    call Main
]

// macro definitions to make the Rtl library happy
#DEFINE BLD_BITS 32
#DEFINE ARCHITECTURE "fox32"

#INCLUDE "rtl.hjk" // Printing functionality
#INCLUDE "rom.hjk" // fox32os bindings
#INCLUDE "os.hjk" // fox32rom bindings
#INCLUDE "pt.jkl" // piece table implementation

/// Returns the number of milliseconds since boot, as provided by the fox32 hardware.
EXTERN FN Millis () : UWORD
#ASM [
Millis:
.global Millis
    in a3, 0x80000706
    ret
]

/// Trigger a breakpoint on fox32, delivering the user to the monitor. Note that fox32os includes a "recovery" option allowing control flow to return after calling this function.
EXTERN FN Trap()
#ASM [
Trap:
.global Trap
    int 10000
    ret
]

/// A data table defining a menu bar in fox32os' required format.
EXTERN menu_bar : Menu
#ASM [
menu_bar:
    // Number of menus
    .db 1
    // "items" and "name" table pairs for each menu
    .dp menu_file_items
    .dp menu_file_name
menu_file_name:
    // length, ASCII string, null
    .db 4 .ds "File" .db 0
menu_file_items:
    // number of menu items
    .db 1
    // width of menu, generally 2 greater than the longest menu item
    .db 6
    // length, ASCII string, null for each menu item
    .db 4 .ds "Save" .db 0
]

/// A fully qualified fox32os file path.
STRUCT FileLocation
    Disk : UBYTE,
    Name : ^UBYTE,
END

/// A file representing standard input and output on fox32os. This is initialized by PreMain and written to by RtlPrint.
stream : ^File = NULLPTR

/// The file path of the currently opened text file.
file_loc : FileLocation

/// A fox32os GUI window handle.
window : Window

/// The mutable character array representing the null terminated title of the window.
window_title := "Hjkl - 0:12345678.123"

/// The text data.
table : Pt // text data

/// The start of the currently visible portion of the text, a.k.a. the top-left character of the window's text buffer. It is an index into `table` and must be within [0, ???].
view_byte_offset : UWORD = 0

/// The cursor position within the text in bytes. It is an index into `table` and must be within [0, table.length()]. (Note the right inclusive bound!) This position is the character (or end of text) that is visually highlighted, and the index at which text is inserted. Note that this means text is inserted *to the left* of the highlighted character.
cursor : UWORD = 0

/// How many columns further the cursor should "stick" to. When moving the cursor vertically across lines, this allows the cursor's horizontal position to be preserved even when navigating across lines that are shorter than that horizontal position.
cursor_desired_offset : UWORD = 0

/// A toggle for navigating by visual lines instead of absolute ones. For example, this changes how moving the cursor up and down behaves on a line that wraps. This toggle is underdocumented and poorly tested currently.
line_mode_visual : UBYTE = TRUE

/// The number of columns in the view buffer
width : UBYTE = 32 * 2

/// The number of rows in the view buffer
height : UBYTE = 20

/// A flag that is set whenever the text buffer needs to be redrawn.
redraw : UBYTE = TRUE

/// Two contiguous 2D arrays representing the visible portion of the text buffer, whose bounds are `width` and `height`. Each array is a left-to-right, top-to-bottom sequence of character byte + control byte pairs. The control byte is a bitflag: 0x1 signals this character is selected by the cursor, 0x2 signals this is a character, 0x4 signals this is the position of the end of the file. The first array is the current text buffer, the next array is the previous text buffer. This means `view_buffer` has a total length of `width * height * 4` bytes. Storing both allows the renderer to only render characters that changed. When the screen is redrawn, the first array is copied to the second.
view_buffer : ^UBYTE = NULLPTR

/// Flag for if the user is currently holding the Ctrl key.
is_ctrl : UBYTE = FALSE

/// A set of ABGR colors for the foreground (text), background, and gutters.
color_fg : ULONG = 0xFF3E3E3E
color_bg : ULONG = 0xFFC1E7F6
color_gutter : ULONG = 0xFFE0F8FF
//color_ultradark : ULONG = 0xFF242018

/// Function used by printing library to emit characters.
FN (RtlPrintCallbackF) RtljPrintCallback (
    IN byte : UBYTE,
    IN context : ^VOID,
)
    buffer : UBYTE[1]
    buffer[0] = byte
    Write(stream, &buffer[0], 1)
END
/// Dummy function required by the runtime library.
FN RtljLockStream (
    IN handle : ^VOID
) : UWORD
END
/// Dummy function required by the runtime library.
FN RtljUnlockStream (
    IN handle : ^VOID,
    IN lockcontext : UWORD,
)
END

/// 1-bit 8x16 images for each hexidecimal digit, for displaying non-printable bytes as "tofu" characters.
small_digits : UINT[16] = {
    075557,
    026227,
    061247,
    061616,
    055711,
    074716,
    034757,
    071122,
    075757,
    075711,
    025755,
    065656,
    034443,
    065556,
    074647,
    074644,
}

/// Gracefully exit the program, cleaning up any resource as necessary.
FN Exit ()
    IF window.FrameBuffer THEN
        DestroyWindow(&window)
    END
    IF view_buffer THEN
        FreeMemory(view_buffer)
    END
    PtFree(&table)
    EndCurrentTask()
END

/// Assure that a given condition is true, or otherwise printing an error message and exiting.
FN Assert (
    IN condition : UBYTE,
    IN label : ^UBYTE,
    IN arg1 : UWORD,
    IN arg2 : UWORD,
)
    IF NOT condition THEN
        RtlPrint("Assertion error: %s\nArg1: %d\nArg2: %d\n", label, arg1, arg2)
        Exit()
    END
END

/// Render the visible portion of the text buffer to `view_buffer`.
FN RenderViewBuffer ()
    inner_offset : UWORD
    i := PtSeek(&table, view_byte_offset, OUT inner_offset)
    cursor_pos := cursor - view_byte_offset
    x := 0
    buffer_pos := 0
    WHILE i < table.Pieces.Length AND buffer_pos < width * height DO
        piece := &table.Pieces.Elems[i]
        c := table.Data.Elems[piece^.Start + inner_offset]
        meta := 2
        IF cursor_pos == 0 THEN
            meta |= 1
        END
        cursor_pos -= 1
        IF c == '\n' THEN
            WHILE x < width DO
                view_buffer[buffer_pos * 2] = ' '
                view_buffer[buffer_pos * 2 + 1] = meta
                meta = 0
                buffer_pos += 1
                x += 1
            END
        ELSE
            view_buffer[buffer_pos * 2] = c
            view_buffer[buffer_pos * 2 + 1] = meta
            buffer_pos += 1
            x += 1
        END
        IF x >= width THEN
            x = 0
        END
        inner_offset += 1
        IF inner_offset >= piece^.Length THEN
            inner_offset = 0
            i += 1
        END
    END
    meta := 4
    IF cursor_pos == 0 THEN
        meta |= 1
    END
    WHILE buffer_pos < width * height DO
        view_buffer[buffer_pos * 2] = ' '
        view_buffer[buffer_pos * 2 + 1] = meta
        meta = 0
        buffer_pos += 1
    END
END

/// Read a fox32os file path into the `FileLocation` object. If the path begins with a disk ID, e.g. "0:hello.txt", that disk ID is written to the file path object. Otherwise, the disk ID is unchanged.
FN ParseFilePath (
    IN fp : ^UBYTE,
    IN loc : ^FileLocation,
)
    IF fp[0] >= '0' AND fp[0] <= '9' AND fp[1] == ':' THEN
        loc^.Disk = fp[0] - '0'
        fp += 2
    END
    loc^.Name = fp
END

/// Save the currently open file to disk.
FN SaveFile ()
    file : File
    sector := Create(file_loc.Name, file_loc.Disk, &file, PtLength(&table))
    // TODO: It is very important to gracefully handle this error rather than crashing.
    Assert(sector, "no room sory", file_loc.Disk, PtLength(&table))
    i := 0
    WHILE i < table.Pieces.Length DO
        piece := &table.Pieces.Elems[i]
        Write(&file, &table.Data.Elems[piece^.Start], piece^.Length)
        i += 1
    END
END

/// Returns the position of the given index within its line. The returned value will always be less than or equal to the input.
FN LineStartOffsetAbsolute (
    IN position : UWORD
) : UWORD
    i := 0
    WHILE i < position AND PtAt(&table, position - i - 1) != '\n' DO
        i += 1
    END
    RETURN i
END

/// Returns how far the end of the line is after the given index.
FN LineEndOffsetAbsolute (
    IN position : UWORD
) : UWORD
    i := 0
    WHILE
        position + i < PtLength(&table) AND
        PtAt(&table, position + i) != '\n'
    DO
        i += 1
    END
    RETURN i
END

// Returns the result of `LineStartOffsetAbsolute` accounting for `line_mode_visual`.
FN LineStartOffset (
    IN position : UWORD
) : UWORD
    offset := LineStartOffsetAbsolute(position)
    IF line_mode_visual THEN
        offset %= width
    END
    RETURN offset
END

/// Move the cursor backward by one character.
FN CursorMoveLeft()
    IF cursor THEN
        cursor -= 1
        redraw = TRUE
    END
    cursor_desired_offset = 0
END

/// Move the cursor forward by one character.
FN CursorMoveRight()
    IF cursor < PtLength(&table) THEN
        cursor += 1
        redraw = TRUE
    END
    cursor_desired_offset = 0
END

/// Move the cursor backward by one row of text.
FN CursorMoveUp()
    len := LineStartOffset(cursor)
    // if we're on the first line, don't move the cursor
    IF cursor == len THEN
        LEAVE
    END
    redraw = TRUE
    prev_len := LineStartOffset(cursor - len - 1)
    desired_len := len + cursor_desired_offset
    move_by := len
    IF prev_len >= desired_len THEN
        move_by = prev_len - cursor_desired_offset
        cursor_desired_offset = 0
    ELSE
        cursor_desired_offset = desired_len - prev_len
    END
    cursor -= move_by + 1
END

/// Move the cursor forward by one row of text.
FN CursorMoveDown()
    next_line_offset := LineEndOffsetAbsolute(cursor)
    next_line := cursor + next_line_offset + 1
    IF next_line > PtLength(&table) THEN
        diff := next_line - PtLength(&table)
        Assert(diff == 1, "CursorMoveDown", diff, cursor)
        LEAVE
    END
    redraw = TRUE
    next_line_len := LineEndOffsetAbsolute(next_line)
    x := LineStartOffset(cursor)
    desired_x := x + cursor_desired_offset
    IF next_line_len >= desired_x THEN
        cursor = next_line + desired_x
        cursor_desired_offset = 0
    ELSE
        cursor = next_line + next_line_len
        cursor_desired_offset = desired_x - next_line_len
    END
END

/// Move the cursor to the first character of the line, not respecting `line_mode_visual`.
FN CursorMoveLineStart()
    offset := LineStartOffsetAbsolute(cursor)
    IF offset THEN
        cursor -= offset
        redraw = TRUE
    END
    cursor_desired_offset = 0
END

/// Move the cursor to the last character of the line, not respecting `line_mode_visual`. This character will always be either the newline character or the end of the file.
FN CursorMoveLineEnd()
    offset := LineEndOffsetAbsolute(cursor)
    IF offset THEN
        cursor += offset
        redraw = TRUE
    END
    cursor_desired_offset = 0
END

/// Move the visible text buffer backwards by the given number of rows.
FN ScrollUp (
    IN lines : UWORD
)
    IF view_byte_offset == 0 THEN LEAVE END
    redraw = TRUE
    WHILE view_byte_offset AND lines DO
        view_byte_offset -= 1
        len := LineStartOffsetAbsolute(view_byte_offset)
        view_byte_offset -= len % width
        lines -= 1
    END
END

/// Move the visible text buffer forwards by the given number of rows.
FN ScrollDown (
    IN lines : UWORD
)
    pt_len := PtLength(&table)
    WHILE lines AND view_byte_offset < pt_len DO
        len := LineEndOffsetAbsolute(view_byte_offset)
        IF view_byte_offset + len >= pt_len THEN LEAVE END
        redraw = TRUE
        IF len >= width THEN
            view_byte_offset += width
        ELSE
            view_byte_offset += len + 1
        END
        lines -= 1
    END
END

/// Respond to a mouse press from the user at a given position relative to the window.
FN HandleMousePress (
    IN x : UINT,
    IN y : UINT,
)
    IF y < 16 THEN // User clicked the title bar
        IF x < 8 THEN Exit() END // User clicked the close window button
        // Otherwise, drag the window
        StartDraggingWindow(&window) // NOTE: This blocks *all* tasks, including ours, until the user releases the mouse click
        LEAVE
    END
    // Clicking the fringes does nothing
    IF x < 8 OR x >= (width + 1) * 8 THEN LEAVE END
    // Otherwise, move the cursor to the mouse position
    IF redraw THEN
        RenderViewBuffer()
    END
    char_x := x / 8 - 1
    char_y := y / 16 - 1
    i := char_y * width + char_x
    num_chars := 0
    WHILE i DO
        IF view_buffer[i * 2 + 1] & 6 THEN
            num_chars += 1
        END
        i -= 1
    END
    new_cursor := view_byte_offset + num_chars
    IF new_cursor != cursor THEN
        cursor = new_cursor
        redraw = TRUE
    END
END

/// Respond to a mouse button release from the user at a given position relative to the window. Currently we do nothing.
FN HandleMouseRelease (
    IN x : UINT,
    IN y : UINT,
)
END

FN HandleCtrlKeyDown (
    IN scancode : UBYTE,
)
    IF scancode == KEY_S THEN
        SaveFile()
    ELSEIF scancode == KEY_C THEN
        Exit()
    ELSEIF scancode == KEY_F THEN
        CursorMoveRight()
    ELSEIF scancode == KEY_B THEN
        CursorMoveLeft()
    ELSEIF scancode == KEY_P THEN
        CursorMoveUp()
    ELSEIF scancode == KEY_N THEN
        CursorMoveDown()
    ELSEIF scancode == KEY_A THEN
        CursorMoveLineStart()
    ELSEIF scancode == KEY_E THEN
        CursorMoveLineEnd()
    ELSEIF scancode == KEY_D THEN
        IF cursor < PtLength(&table) THEN
            PtDelete(&table, cursor)
            redraw = TRUE
        END
        cursor_desired_offset = 0
    ELSEIF scancode == KEY_U THEN
        ScrollUp((height + 1) / 2)
    ELSEIF scancode == KEY_V THEN
        ScrollDown((height + 1) / 2)
    END
END

/// Respond to a key being pressed by the user.
FN HandleKeyDown (
    IN scancode : UBYTE,
)
    IF scancode == KEY_BACKSPACE THEN
        IF cursor THEN
            cursor -= 1
            PtDelete(&table, cursor)
            redraw = TRUE
        END
        cursor_desired_offset = 0
    ELSEIF scancode == KEY_CTRL THEN
        is_ctrl = TRUE
    ELSEIF scancode == KEY_LSHIFT OR scancode == KEY_RSHIFT THEN
        ShiftPressed()
    ELSEIF scancode == KEY_CAPS THEN
        CapsPressed()
    ELSEIF scancode == KEY_UP THEN
        CursorMoveUp()
    ELSEIF scancode == KEY_DOWN THEN
        CursorMoveDown()
    ELSEIF scancode == KEY_LEFT THEN
        CursorMoveLeft()
    ELSEIF scancode == KEY_RIGHT THEN
        CursorMoveRight()
    ELSEIF is_ctrl THEN
        HandleCtrlKeyDown(scancode)
    ELSEIF
        // TODO: Support keypad?
        0x02 <= scancode AND scancode <= 0x0D
     OR 0x10 <= scancode AND scancode <= 0x1C // notably, we include the Enter key in this range
     OR 0x1E <= scancode AND scancode <= 0x29
     OR 0x2B <= scancode AND scancode <= 0x35
     OR scancode == 0x39
    THEN
        c := ScancodeToAscii(scancode) // this is affected by ShiftPressed(), etc.
        PtInsert(&table, cursor, c)
        cursor += 1
        redraw = TRUE
        cursor_desired_offset = 0
    END
END

/// Respond to a key being released by the user.
FN HandleKeyUp (
    IN scancode : UBYTE,
)
    IF scancode == KEY_LSHIFT OR scancode == KEY_RSHIFT THEN
        ShiftReleased()
    ELSEIF scancode == KEY_CTRL THEN
        is_ctrl = FALSE
    END
END

/// Respond to a menu item being selected by the user.
FN HandleMenuClick (
    IN menu : UWORD,
    IN item : UWORD,
)
    Assert(menu == 0 AND item == 0, "invalid menu choice", menu, item)
    SaveFile()
END

/// Write a pixel of a given color to the window.
FN DrawPixel (
    IN x : UINT,
    IN y : UINT,
    IN color : ULONG,
)
    window.FrameBuffer[window.Width * y + x] = color
END

/// Render an 8x16 "tofu" character.
FN DrawReplacementTile (
    IN c : UBYTE,
    IN x : UINT,
    IN y : UINT,
    IN fg_color : ULONG,
    IN bg_color : ULONG,
)
    i := 1
    WHILE i < 8 DO
        DrawPixel(x + i, y, bg_color)
        DrawPixel(x + i, y + 1, fg_color)
        DrawPixel(x + i, y + 2, bg_color)
        DrawPixel(x + i, y + 8, bg_color)
        DrawPixel(x + i, y + 14, bg_color)
        DrawPixel(x + i, y + 15, fg_color)
        i += 1
    END
    i = 0
    WHILE i < 16 DO
        DrawPixel(x, y + i, bg_color)
        i += 1
    END
    i = 2
    WHILE i < 15 DO
        DrawPixel(x + 1, y + i, fg_color)
        DrawPixel(x + 2, y + i, bg_color)
        DrawPixel(x + 6, y + i, bg_color)
        DrawPixel(x + 7, y + i, fg_color)
        i += 1
    END
    i = 0
    WHILE i <= 1 DO
        half := (c >> (i * 4)) & 0xF
        bit_image := small_digits[half]
        dy := 0
        WHILE dy < 5 DO
            dx := 0
            WHILE dx < 3 DO
                bit_color := bg_color
                IF (bit_image >> (14 - dy * 3 - dx)) & 1 THEN
                    bit_color = fg_color
                END
                DrawPixel(x + 3 + dx, y + 9 + dy - i * 6, bit_color)
                dx += 1
            END
            dy += 1
        END
        i += 1
    END
END

/// Render the background and gutters.
FN DisplayInitial ()
    FillWindow(&window, color_bg)
    y := 0
    WHILE y < height DO
        DrawFontTileToOverlay(' ', 0, y * 16 + 16, 0, color_gutter, window.Overlay)
        DrawFontTileToOverlay(' ', (width + 1) * 8, y * 16 + 16, 0, color_gutter, window.Overlay)
        y += 1
    END
END

/// Called whenever `redraw` is set. Find all the tiles that have changed since last redraw, and draw each to the screen.
FN DisplayViewBuffer ()
    y := 0
    WHILE y < height DO
        x := 0
        WHILE x < width DO
            i := y * width + x
            char := view_buffer[i * 2]
            meta := view_buffer[i * 2 + 1]
            IF
                char == view_buffer[i * 2 + width * height * 2] AND
                meta == view_buffer[i * 2 + 1 + width * height * 2]
            THEN
                x += 1
                CONTINUE
            END
            display_x := (x + 1) * 8
            display_y := (y + 1) * 16
            color1 := color_fg
            color2 := color_bg
            IF meta & 1 THEN
                temp := color1
                color1 = color2
                color2 = temp
            END
            IF 0x20 <= char AND char <= 0x7E THEN
                DrawFontTileToOverlay(char, display_x, display_y, color1, color2, window.Overlay)
            ELSE
                DrawReplacementTile(char, display_x, display_y, color1, color2)
            END
            x += 1
        END
        y += 1
    END
END

/// Code that runs before `Main`, things that would be handled by the C language before main.
FN PreMain (
    IN stream_ : ^File,
)
    stream = stream_
    file_loc.Name = NULLPTR
    window.FrameBuffer = NULLPTR
    PtNull(&table)
END

/// The entry point for our application.
FN Main (
    IN stream_ : ^File,
    IN arg0 : ^UBYTE,
    IN arg1 : ^UBYTE,
    IN arg2 : ^UBYTE,
    IN arg3 : ^UBYTE,
)
    PreMain(stream_)
    IF NOT arg0 OR arg1 | arg2 | arg3 THEN
        RtlPrint("Usage: hjkl <file>\n")
        Exit()
    END

    // Get filepath to open
    file_loc.Disk = GetCurrentDiskId()
    ParseFilePath(arg0, &file_loc)

    // Open and read file to memory
    IF TRUE THEN
        file : File
        IF NOT Open(file_loc.Name, file_loc.Disk, &file) THEN
            RtlPrint("Could not find file %s\n", file_loc.Name)
            Exit()
        END
        PtInit(&file, &table)
        RtlPrint("opened %d:%s of %d bytes\n", file_loc.Disk, file_loc.Name, GetSize(&file))
    END

    // create window title
    IF TRUE THEN
        window_title[7] = '0' + file_loc.Disk
        i := 0
        WHILE TRUE DO
            window_title[i + 9] = file_loc.Name[i]
            IF file_loc.Name[i] == 0 THEN BREAK END
            i += 1
        END
    END

    // allocate the view buffer and imagine the previous frame is blank
    view_buffer = AllocateMemory(width * height * 4)
    IF TRUE THEN
        last_frame := view_buffer + width * height * 2
        i := 0
        WHILE i < width * height DO
            last_frame[i * 2] = ' '
            last_frame[i * 2 + 1] = 0
            i += 1
        END
    END

    window_width := (width + 2) * 8
    window_height := height * 16
    NewWindow(&window, window_title, window_width, window_height, (640 - window_width) / 2, (480 - window_height) / 2, &menu_bar, 0)
    DisplayInitial()

    WHILE TRUE DO
        // Poll events until our queue is empty
        WHILE TRUE DO
            ev : Event
            GetNextWindowEvent(&window, &ev)
            IF ev.Type == EVENT_TYPE_EMPTY THEN
                BREAK
            ELSEIF ev.Type == EVENT_TYPE_MOUSE_CLICK THEN
                HandleMousePress(ev.Body.Pos.X, ev.Body.Pos.Y)
            ELSEIF ev.Type == EVENT_TYPE_MOUSE_RELEASE THEN
                HandleMouseRelease(ev.Body.Pos.X, ev.Body.Pos.Y)
            ELSEIF ev.Type == EVENT_TYPE_KEY_DOWN THEN
                HandleKeyDown(ev.Body.Scancode)
            ELSEIF ev.Type == EVENT_TYPE_KEY_UP THEN
                HandleKeyUp(ev.Body.Scancode)
            ELSEIF ev.Type == EVENT_TYPE_MENU_BAR_CLICK THEN
                MenuBarClickEvent(&menu_bar, ev.Body.Pos.X)
            ELSEIF ev.Type == EVENT_TYPE_MENU_UPDATE THEN
                MenuUpdateEvent(&menu_bar, ev.Body.MenuChoice.Menu, ev.Body.MenuChoice.Item)
            ELSEIF ev.Type == EVENT_TYPE_MENU_CLICK THEN
                HandleMenuClick(ev.Body.MenuChoice.Menu, ev.Body.MenuChoice.Item)
            ELSEIF ev.Type == EVENT_TYPE_MENU_ACK THEN
                CloseMenu(&menu_bar)
            ELSE
                // We got a bad type. Ignore it?
                RtlPrint("got unknown event %u\n", ev.Type)
            END
        END
        // Redraw the text buffer
        IF redraw THEN
            redraw = FALSE
            t0 := Millis()
            RenderViewBuffer()
            t1 := Millis()
            DisplayViewBuffer()
            t2 := Millis()
            CopyMemoryBytes(view_buffer, view_buffer + width * height * 2, width * height * 2)
            t3 := Millis()
            // RtlPrint("%d %d %d\n", t1-t0, t2-t1, t3-t2)
            // PtDebug(&table)
        END
        YieldTask()
    END
END
